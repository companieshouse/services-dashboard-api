resource "aws_sqs_queue" "data" {
  name                        = "${var.service}-${var.environment}-queue.fifo"
  fifo_queue                  = true
  content_based_deduplication = true
  visibility_timeout_seconds  = var.data_queue_visibility_timeout_seconds
  message_retention_seconds   = var.message_retention_seconds

  redrive_policy = jsonencode({
    deadLetterTargetArn = aws_sqs_queue.dead_letter.arn
    maxReceiveCount     = var.data_queue_max_receive_count
  })

  tags = {
    Name        = "${var.service}-${var.environment}-queue"
    Environment = var.environment
    Service     = var.service
  }
}

resource "aws_sqs_queue_policy" "data" {
  queue_url = aws_sqs_queue.data.id
  policy    = data.aws_iam_policy_document.data_queue.json
}

resource "aws_sqs_queue" "dead_letter" {
  name                        = "${var.service}-${var.environment}-dead-letter-queue.fifo"
  fifo_queue                  = true
  content_based_deduplication = true
  message_retention_seconds   = var.message_retention_seconds

  tags = {
    Name        = "${var.service}-${var.environment}-dead-letter-queue"
    Environment = var.environment
    Service     = var.service
  }
}
